#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ -z "$BROWSER" && "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

if [[ -z "$EDITOR" ]]; then
  export EDITOR='nvim'
fi
if [[ -z "$VISUAL" ]]; then
  export VISUAL='nvim'
fi
if [[ -z "$PAGER" ]]; then
  export PAGER='less'
fi

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
  export LC_CTYPE='en_US.UTF-8'
fi

# export LC_ALL=en_US.UTF-8 # otherwise perl complains in vscode's terminal

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
# (N) = NULL_GLOB = optional, no error
path=(
  $HOME/.cargo/bin(N)
  $HOME/{,s}bin(N)
  /opt/{local}/{,s}bin(N) # homebrew,local -> local since otherwise `brew shellenv` below does nothing, but is needed for setting HOMEBREW_PREFIX, MANPATH etc.
  /usr/local/{,s}bin(N)
  $path
)
#  /usr/local/{bin,sbin}
#  /usr/sbin

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X to enable it.
if [[ -z "$LESS" ]]; then
  export LESS='-g -i -M -R -+S -w -X -z-4'
fi

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if [[ -z "$LESSOPEN" ]] && (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi


# customizations: paths, init scripts...
if [[ "$OSTYPE" == darwin* ]]; then
  [[ $(uname -m) == "arm64" ]] && eval "$(/opt/homebrew/bin/brew shellenv)"
  if hash brew 2>/dev/null; then
    # use GNU tools instead, see https://www.topbug.net/blog/2013/04/14/install-and-use-gnu-command-line-tools-in-mac-os-x/
    path=(
      $HOMEBREW_PREFIX/opt/{coreutils,findutils,grep,gnu-sed}/libexec/gnubin(N)
      $path
    )
    # manpath executable is used to generate path from PATH (but only if MANPATH is not set)
    # export MANPATH="/usr/local/opt/coreutils/libexec/gnuman:$MANPATH"
  fi
elif [[ "$OSTYPE" == linux* ]]; then
  path=(
    /snap/bin(N)
    $path
  )
fi

# ~/dotfiles/bin mapped to ~/.local/bin instead of just ~/bin since ~/.local is already used by nvim, delta, gh, heroku
export PATH=$HOME/.local/bin:$PATH

# node version manager via https://github.com/nvm-sh/nvm#install--update-script
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh" # This loads nvm and node installed with it

# Added by OrbStack: command-line tools and integration
# Comment this line if you don't want it to be added again.
source ~/.orbstack/shell/init.zsh 2>/dev/null || :
